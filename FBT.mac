!Four Bar Truss
!Units:N,mm,s   
    
finish$/clear   
/filname, Four_Bar_Truss, 1  
    
!****************************************************   
!Pre-processing 
    
/prep7  
    
!Parameter Definition   
!---------------------------------  
!iter_num = 1$/title, Iteration%iter_num%
secarea1 = 100$secarea2 = 100$secarea3 = 100$secarea4 = 100

!Elements Properites
!--------------------------------- 
et, 1, link180  
sectype, 1, link, , ,$secdata, secarea1 
sectype, 2, link, , ,$secdata, secarea2 
sectype, 3, link, , ,$secdata, secarea3 
sectype, 4, link, , ,$secdata, secarea4 
    
!Material Properties
!---------------------------------  
mp, ex, 1, 2.07e5$mp, prxy, 1, 0.3   
    
!Geometry Properties
!---------------------------------  
!Keypoints  
k, 1, 0, 0, 0$k, 2, 707, 0, 0$k, 3, 1414, 707, 0$k, 4, 0, 707, 0 
!Lines  
l, 1, 2$l, 2, 3$l, 3, 4$l, 4, 2 
    
!Load & Boundary Conditions 
!---------------------------------  
!Load   
fk, 2, fy, -2500$fk, 3, fy, -2500
!Boundary Conditions
ksel, s, kp, , 1, 4, 3  
dk, all, ux, 0$dk, all, uy, 0$dk, all, uz, 0$dk, all, rotx, 0$dk, all, roty, 0  
allsel, all 
    
!Mesh   
!---------------------------------  
lsel, s, line, , 1$latt, 1, , 1, , , , 1$allsel, all
lsel, s, line, , 2$latt, 1, , 1, , , , 2$allsel, all
lsel, s, line, , 3$latt, 1, , 1, , , , 3$allsel, all
lsel, s, line, , 4$latt, 1, , 1, , , , 4$allsel, all
lesize, all, , , 1$lmesh,all
    
finish  
    
!****************************************************   
!Solve  
/solu   
antype, 0$solve 
    
finish  
    
!****************************************************   
!Post-Processing
    
/post1  
!Interface Display  
pldisp, 1   
    
!Axis Force 
!---------------------------------  
*get, axisforce1, elem, 1, smisc, 1 
*get, axisforce2, elem, 2, smisc, 1 
*get, axisforce3, elem, 3, smisc, 1 
*get, axisforce4, elem, 4, smisc, 1 
*cfopen, elemaxisforce, dat, ,  
*vwrite, axisforce1, axisforce2, axisforce3,  axisforce4
(f15.6) 
*cfclos 
    
!Axis Sterss
!---------------------------------  
!这里*get命令中的etab选项比较有意思，后面的lab选项并不是某种单元类型输出中的名称  
!而是已经定义好的etable的名称，这里的axstress就是提前定义好的link180单元的轴向应力表 
etable, axstress, ls, 1 
*get, axisstress1, elem, 1, etab, axstress  
*get, axisstress2, elem, 2, etab, axstress  
*get, axisstress3, elem, 3, etab, axstress  
*get, axisstress4, elem, 4, etab, axstress 
*cfopen, elemaxisstress, dat, , 
*vwrite, axisstress1, axisstress2, axisstress3, axisstress4  
(f15.6) 
*cfclos 
    
!Node Displacement  
!---------------------------------  
ux1 = ux(1)$ux2 = ux(2)$ux3 = ux(3)$ux4 = ux(4) 
uy1 = uy(1)$uy2 = uy(2)$uy3 = uy(3)$uy4 = uy(4) 
uz1 = uz(1)$uz2 = uz(2)$uz3 = uz(3)$uz4 = uz(4) 
*cfopen, nodedisp, dat, ,   
*vwrite, ux1, uy1, uz1, ux2, uy2, uz2, ux3, uy3, uz3, ux4, uy4, uz4 
(f15.6,f15.6,f15.6) 
*cfclos 
    
finish  
